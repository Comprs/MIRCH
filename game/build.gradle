buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.1.0"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "com.diffplug.gradle.spotless"

    version = "1.0"
    ext {
        appName = "MIRCH"
        gdxVersion = "1.9.5"
        sqliteJbdcVersion = "3.16.1"
    }

    repositories {
        jcenter()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    spotless {
        java {
            eclipseFormatFile "../style.xml"
        }
    }
}

project(":core") {
    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "org.xerial:sqlite-jdbc:$sqliteJbdcVersion"
    }
}

project(":desktop") {
    dependencies {
        compile project(":core")

        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":tests") {
    sourceSets.test.java.srcDirs = ["src"]

    dependencies {
        compile project(":core")

        testCompile "junit:junit:4.12"
        testCompile "org.mockito:mockito-core:2.2.7"
        testCompile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}
